apply plugin: 'groovy'

repositories { jcenter() }

dependencies {
    compile gradleApi()
    compile 'com.puppycrawl.tools:checkstyle:6.1.1'
    compile 'net.sourceforge.pmd:pmd:5.1.3'
}

group = 'com.noveogroup.android'
version = '1.0.1'
description = 'Static code analysis plugin for Android project.'

task wrapper(type: Wrapper) {
    group 'Build Setup'
    description 'Initializes Gradle Wrapper.'
    gradleVersion = '2.2.1'
}

project.ext {
    name = 'Android Check Plugin'
    website = 'https://github.com/noveogroup/android-check'
    pom = {
        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'http://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }
        scm {
            url 'https://github.com/noveogroup/android-check'
            connection 'scm:git@github.com:noveogroup/android-check.git'
            developerConnection 'scm:git@github.com:noveogroup/android-check.git'
        }
        issueManagement {
            system 'GitHub'
            url 'https://github.com/noveogroup/android-check/issues'
        }
        developers {
            developer {
                name 'Pavel Stepanov'
                url 'https://github.com/stefan-nsk'
                roles {
                    role 'architect'
                    role 'developer'
                }
            }
        }
    }
    bintray = {
        user = project.hasProperty('bintray.user') ? project['bintray.user'] : 'anonymous'
        key = project.hasProperty('bintray.key') ? project['bintray.key'] : 'api-key'
        repo = 'android-check'
        tags = ['android', 'plugin', 'check']
        licenses = ['MIT']
        issueTracker = 'https://github.com/noveogroup/android-check/issues'
        vcs = 'https://github.com/noveogroup/android-check'
        dryRun = false  // whether to run this as dry-run, without deploying
        publish = false // if version should be auto published after an upload
    }
}

apply from: rootProject.file('gradle/publish.gradle')
